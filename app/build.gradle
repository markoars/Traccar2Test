apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.1'

    defaultConfig {
        applicationId "com.gpsbase.client"
        buildConfigField "boolean", "HIDDEN_APP", "false"
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 41
        versionName '1.0'
    }

    lintOptions {
        checkReleaseBuilds false
    }

    productFlavors {
        regular
        google
        hidden {
            applicationId "com.gpsbase.client.hidden"
            buildConfigField "boolean", "HIDDEN_APP", "true"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }
}

dependencies {
    compile 'com.android.support:design:26.0.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.4.2'
    googleCompile 'com.google.firebase:firebase-core:11.2.0'
    googleCompile 'com.google.firebase:firebase-crash:11.2.0'
}

if (getGradle().getStartParameter().getTaskRequests().toString().contains("Google")) {
    apply plugin: 'com.google.gms.google-services'

    task copyJson(type: Copy) {
        from '../../gpsbase-shared/google-services'
        into '.'
        include 'gpsbase-client-android.json'
        rename('gpsbase-client-android.json', 'google-services.json')
    }

    afterEvaluate {
        preBuild.dependsOn copyJson
    }
}
